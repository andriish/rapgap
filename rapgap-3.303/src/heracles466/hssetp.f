
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

      SUBROUTINE HSSETP
C---SETTING OF ELECTROWEAK PARAMETERS
C*****
C   MODIFIED VERSION
C   HJM 1/12/89
C   HS 7/7/91
C*****
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
C
      COMPLEX*16 HSSRWW,HSSRZZ
      COMPLEX*16 CMW2,CMZ2
      EXTERNAL HSSRWW,HSSRZZ
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSCUTS/ XMIN,XMAX,Q2MIN,Q2MAX,YMIN,YMAX,WMIN,GMIN
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      COMMON /HSPARM/ POLARI,LLEPT,LQUA
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSDELR/ DELTAR,AGF0,DRHOT,DALPMZ,XGMT,ALPQCD,BTOP4,DRPIW2
      COMMON /HSCBMS/ CMW2,CMZ2
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSSMCP/ VAFI(2,3,2),AFIJ(3,2,2),BFIJ(3,2,2),FLIND(2,3,2,2)
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSKNCC/ SXNRCC,SX1NCC
      COMMON /HSIRCT/ DELEPS,DELTA,EGMIN,IOPEGM
      COMMON /HSPDFQ/ QU,QBU,QD,QBD,QS,QBS,QC,QBC,QB,QBB,QT,QBT
C
C---DEFINE CONSTANTS
      PI=4D0*DATAN(1D0)
      ALPHA=1D0/137.0359895D0
      ALP1PI=ALPHA/PI
      ALP2PI=ALPHA/2D0/PI
      ALP4PI=ALPHA/4D0/PI
      E=DSQRT(4D0*PI*ALPHA)
      GF=1.166389D-5
      AGF0=PI*ALPHA/GF/DSQRT(2D0)
      SXNORM=PI*ALPHA*ALPHA/2D0*3.8938D5
      SX1NRM=ALPHA*ALPHA*ALPHA/16D0/PI*3.8938D5

C---DEFINE PARAMETERS OF THE ELECTROWEAK STANDARD MODEL
      ME=.51099906D-3
      MMY=.105658387D0
      MTAU=1.7841D0
      MU=.062D0
      MD=.083D0
      MS=.215D0
      MC=1.5D0
      MB=4.5D0
      IF (LPAR(5).NE.1) THEN
        MT=175.0D0
        MH=150.0D0
      ENDIF
      MH2=MH*MH
      ME2=ME*ME
      MMY2=MMY*MMY
      MTAU2=MTAU*MTAU
      MU2=MU*MU
      MD2=MD*MD
      MS2=MS*MS
      MC2=MC*MC
      MB2=MB*MB
      MT2=MT*MT
C
      IF (LPAR(5).NE.1) MZ=91.1867D0
      MZ2=MZ*MZ
      IF (LPAR(4).GT.1) THEN
        MW=HSPGFX()
      ELSE
        IF (LPAR(5).NE.1) MW=80.330D0
        MWKEEP=MW
        MW1=HSPGFX()
        MW=MWKEEP
      ENDIF
      MW2=MW*MW
      CW=MW/MZ
      CW2=CW*CW
      SW2=1D0-CW2
      SW=DSQRT(SW2)
      WWIDTH=DIMAG(HSSRWW(MW2))/MW
      ZWIDTH=DIMAG(HSSRZZ(MZ2))/MZ
      CMW2=MW*DCMPLX(MW,-WWIDTH)
      CMZ2=MZ*DCMPLX(MZ,-ZWIDTH)

C---NORMALIZATION OF NC AND CC CROSS SECTIONS
C---ON-MASS SHELL SCHEME
      IF (LPAR(4).EQ.1) THEN
        B=1D0/4D0/CW/SW
        SXNRCC=SXNORM/SW2/SW2
        SX1NCC=SX1NRM/SW2/SW2
        ELSE
C---MODIFIED ON-MASS SHELL SCHEME (NORMALIZATION TO G-MU)
        B=MZ/SQRT(AGF0)/4D0
        SXNRCC=SXNORM*MW2*MW2/AGF0/AGF0
        SX1NCC=SX1NRM*MW2*MW2/AGF0/AGF0
        IF (LPAR(4).EQ.3) B=B*SQRT(1D0-DELTAR)
      ENDIF
C---DEFINE FERMION GAUGE BOSON COUPLING CONSTANTS
      VAFI(2,1,1)=0D0
      VAFI(2,2,1)=0D0
      VAFI(2,3,1)=0D0
      VAFI(2,1,2)=-B
      VAFI(2,2,2)=B
      VAFI(2,3,2)=-B
      VAFI(1,1,1)=1D0
      VAFI(1,2,1)=-2D0/3D0
      VAFI(1,3,1)=1D0/3D0
      VAFI(1,1,2)=B*(4D0*SW2-1D0)
      VAFI(1,2,2)=B*(1D0-8D0*SW2/3D0)
      VAFI(1,3,2)=B*(4D0*SW2/3D0-1D0)

C----------------------------------------------------------------------
C---MASSES USED FOR HARD BREMSSTRAHLUNG KINEMATICS
      MEI=ME
      MEF=ME

C---QUARK MASSES USED AS REGULATORS IN THE LEPTONIC BREMSSTRAHLUNG
C---DO NOT USE FOR VERY SMALL X
      MPRO=938.28D-3
      MPRO2=MPRO*MPRO
      MQI=MU
      MQF=MU
      MEF2=MEF*MEF
      MQF2=MQF*MQF
      MEI2=MEI*MEI
      MQI2=MQI*MQI
C
      IGAMMA = 1
      IZ = 2
      IEL = 1
      IFU = 2
      IFD = 3
      INDV = 1
      INDA = 2
C
      DO 1 IF=IEL,IFD
        DO 1 IB1=IGAMMA,IZ
          DO 1 IB2=IGAMMA,IZ
          FLIND(INDV,IF,IB1,IB2)=
     *     2D0*(VAFI(INDV,IF,IB1)*VAFI(INDV,IF,IB2)
     *         +VAFI(INDA,IF,IB1)*VAFI(INDA,IF,IB2))
    1 CONTINUE
      DO 2 IF=IEL,IFD
        DO 2 IB1=IGAMMA,IZ
          DO 2 IB2=IGAMMA,IZ
          FLIND(INDA,IF,IB1,IB2)=
     *     2D0*(VAFI(INDV,IF,IB1)*VAFI(INDA,IF,IB2)
     *         +VAFI(INDA,IF,IB1)*VAFI(INDV,IF,IB2))
    2 CONTINUE
C
      DO 3 IVB1 = IGAMMA, IZ
       DO 3 IVB2 = IGAMMA, IZ
        DO 3 IFERM = IFU, IFD
        AFIJ(IFERM,IVB1,IVB2)=FLIND(INDV,IFERM,IVB1,IVB2)
     *  *(FLIND(INDV,IEL,IVB1,IVB2) - POLARI*FLIND(INDA,IEL,IVB1,IVB2))
        BFIJ(IFERM,IVB1,IVB2)=FLIND(INDA,IFERM,IVB1,IVB2)
     *  *(FLIND(INDA,IEL,IVB1,IVB2) - POLARI*FLIND(INDV,IEL,IVB1,IVB2))
    3 CONTINUE
C
      RETURN
      END
