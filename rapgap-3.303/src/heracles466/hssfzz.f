CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

        FUNCTION HSSFZZ(Q2)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C       HSSFZZ(Q2): BEITRAEGE ZUR Z0-BOSONSELBSTENERGIE
C                   'REGULAERER' ANTEIL
C       D O U B L E P R E C I S I O N
C-----------------------------------------------------------------------
C       09.11.83
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMPLEX*16 HSSFZZ,HSFONE,NYANT,ANTZH
     *          ,FQWW,FQZH,FQEE,FQMYMY,FQTATA
     *          ,FQUU,FQCC,FQTT,FQDD,FQSS,FQBB
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
        DMHZ2=MH2-MZ2
        SMHZ2=MH2+MZ2
        FQWW=HSFONE(Q2,MW,MW)
        FQZH=HSFONE(Q2,MZ,MH)
        FQEE=HSFONE(Q2,ME,ME)
        FQMYMY=HSFONE(Q2,MMY,MMY)
        FQTATA=HSFONE(Q2,MTAU,MTAU)
        FQUU=HSFONE(Q2,MU,MU)
        FQCC=HSFONE(Q2,MC,MC)
        FQTT=HSFONE(Q2,MT,MT)
        FQDD=HSFONE(Q2,MD,MD)
        FQSS=HSFONE(Q2,MS,MS)
        FQBB=HSFONE(Q2,MB,MB)
        DLHW=DLOG(MH/MW)
        DLZW=-DLOG(CW)
        DLZH=DLOG(MZ/MH)
        DLEW2=DLOG(ME2/MW2)
        DLYW2=DLOG(MMY2/MW2)
        DLAW2=DLOG(MTAU2/MW2)
        DLUW2=DLOG(MU2/MW2)
        DLCW2=DLOG(MC2/MW2)
        DLTW2=DLOG(MT2/MW2)
        DLDW2=DLOG(MD2/MW2)
        DLSW2=DLOG(MS2/MW2)
        DLBW2=DLOG(MB2/MW2)
        NYANT=0D0
        IF(Q2 .NE. 0D0) GOTO 10
         ANTZH=0.5D0*SMHZ2+MH2*MZ2/DMHZ2*2D0*DLZH
        GOTO 20
10      ANTZH=FQZH/Q2*DMHZ2*DMHZ2
        IF(Q2 .LT. 0D0) NYANT=5D0/3D0-DLOG(-Q2/MW2)
        IF(Q2 .GT. 0D0) NYANT=DCMPLX(5D0/3D0-DLOG(Q2/MW2),PI)
20      S2C224=1D0/(24D0*SW2*CW2)
        S2C208=3D0*S2C224
        S2_L=8D0*SW2*(SW2-0.5D0)+1D0
        S2_2=SW2/0.28125D0*(SW2-0.75D0)+1D0
        S2_1=SW2/1.125D0*(SW2-1.5D0)+1D0
        HSSFZZ=+ALP1PI*(
     G  +DFLOAT(LPAR(15))/(12D0*SW2)*
     G   (-(Q2/1.5D0+(2D1*MW2+1D1*Q2)*FQWW                     )*CW2
     G    +(3D0*MW2*FQWW+Q2/6D0
     G      -MH2*DLHW-MZ2*DLZW
     G      +0.25D0*(1D1*MZ2-2D0*MH2+Q2)*
     G         (1D0+SMHZ2/DMHZ2*DLZH
     G             -DLZW-DLHW      +FQZH   )
     G      +0.25D0*ANTZH                                      )/CW2
     G    +(Q2/6D0+(2D0*MW2+0.25D0*Q2)*FQWW)*(CW2-SW2)*(CW2-SW2)/CW2)
     L  +S2C224*
     L   (S2_L*((2D0*ME2+Q2)*FQEE-Q2/3D0)    -3D0*ME2*FQEE    )
     L  +S2C224*
     L   (S2_L*((2D0*MMY2+Q2)*FQMYMY-Q2/3D0) -3D0*MMY2*FQMYMY )
     L  +S2C224*
     L   (S2_L*((2D0*MTAU2+Q2)*FQTATA-Q2/3D0)-3D0*MTAU2*FQTATA)
     N  +S2C224*Q2*(NYANT+DLEW2)
     N  +S2C224*Q2*(NYANT+DLYW2)
     N  +S2C224*Q2*(NYANT+DLAW2) )
        HSSFZZ=HSSFZZ+ALP1PI*(
     2  +S2C208*
     2   (S2_2*((2D0*MU2+Q2)*FQUU-Q2/3D0)    -3D0*MU2*FQUU    )
     2  +S2C208*
     2   (S2_2*((2D0*MC2+Q2)*FQCC-Q2/3D0)    -3D0*MC2*FQCC    )
     2  +S2C208*
     2   (S2_2*((2D0*MT2+Q2)*FQTT-Q2/3D0)    -3D0*MT2*FQTT    )
     1  +S2C208*
     1   (S2_1*((2D0*MD2+Q2)*FQDD-Q2/3D0)    -3D0*MD2*FQDD    )
     1  +S2C208*
     1   (S2_1*((2D0*MS2+Q2)*FQSS-Q2/3D0)    -3D0*MS2*FQSS    )
     1  +S2C208*
     1   (S2_1*((2D0*MB2+Q2)*FQBB-Q2/3D0)    -3D0*MB2*FQBB    ) )
        RETURN
        END
