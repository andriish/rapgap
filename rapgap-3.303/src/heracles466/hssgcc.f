C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C     CROSS SECTION AND
C     VIRTUAL AND SOFT BREMSSTRAHLUNG CORRECTIONS
C     FOR DEEP INELASTIC ELECTRON PROTON SCATTERING
C     VIA  CHARGED CURRENT
C     AT HERA
C
C     AUTHOR: H.SPIESBERGER, DEC 1990 (20.03.87)
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C     CROSS SECTION FOR CHARGED CURRENT ELECTRON PROTON SCATTERING
C
      FUNCTION HSSGCC (X,Y,LL,POL,LQ)
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMPLEX*16 HSSRWW,HSENUW,HSDUWQ,HSDUWA,CMW2,CMZ2,CMW20,CMZ20
     *          ,PIW,CENW,CDUWQ,CDUWA
     *          ,HSWG1L,HSWG2L,CWG2QL,CWG1AL
     *          ,HSWG1R,HSWG2R,CWG1QR,CWG2QR,CWG1AR,CWG2AR
     *          ,HSIWZ1,HSIWZ2
      COMMON /HSUNTS/ LUNTES,LUNDAT,LUNIN,LUNOUT,LUNRND
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSKNCC/ SXNRCC,SX1NCC
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      COMMON /HSPARM/ POLARI,LLEPT,LQUA
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSCBMS/ CMW2,CMZ2
      COMMON /HSSMCP/ VAFI(2,3,2),AFIJ(3,2,2),BFIJ(3,2,2),FLIND(2,3,2,2)
      COMMON /HSDELR/ DELTAR,AGF0,DRHOT,DALPMZ,XGMT,ALPQCD,BTOP4,DRPIW2
      COMMON /HSCUMS/ CQP(12)
      COMMON /HSPDFQ/ QU,QBU,QD,QBD,QS,QBS,QC,QBC,QB,QBB,QT,QBT
      COMMON /HSWGTC/ IWEIGS
C
C---EXTERNAL WEIGHT
      IACPT=1
      IF (IWEIGS.GT.0) THEN
        CALL HSWGTX(X,Y,IACPT)
        IF (IACPT.EQ.0) THEN
          HSSGCC=0D0
          RETURN
        ENDIF
      ENDIF
C
C---PREPARE GENERAL FACTORS, CALL PARTON DISTRIBUTIONS
      T=-(SP-MEI2-MPRO2)*X*Y
      RPOL=(1D0+LL*POLARI)/2D0
      SN0CC=SXNRCC*SP*X/(T-MW2)/(T-MW2)*RPOL
      FQA=(1D0-Y)*(1D0-Y)
      CALL HSDELO(X,Y)
      CALL HSPVER(X,-T)
      DO 10 I=1,12
   10 CQP(I)=0D0
 
C
C---BORN CROSS SECTION
      IF (LPAR(2).EQ.0) THEN
        IF (LL.EQ.-1) THEN
          CQP(1) =CQP(1)  + QU
          CQP(4) =CQP(4)  + QBD*FQA
          CQP(6) =CQP(6)  + QBS*FQA
          CQP(7) =CQP(7)  + QC
C          CQP(10)=CQP(10) + QBB*FQA
Chs: no b-quarks
          CQP(11)=CQP(11) + QT
        ELSEIF(LL.EQ.1) THEN
          CQP(2) =CQP(2)  + QBU
          CQP(3) =CQP(3)  + QD*FQA
          CQP(5) =CQP(5)  + QS*FQA
          CQP(8) =CQP(8)  + QBC
C          CQP(9) =CQP(9)  + QB*FQA
Chs: no b-quarks
          CQP(12)=CQP(12) + QBT
        ELSE
          WRITE(LUNOUT,*) ' ERROR IN HSSGCC: WRONG LEPTON CHARGE = ',LL
          STOP
        ENDIF
        IF (LQ.GT.0) THEN
          HSSGCC=CQP(2*LQ-1)*SN0CC
        ELSEIF (LQ.LT.0) THEN
          HSSGCC=CQP(-2*LQ)*SN0CC
        ELSEIF (LQ.EQ.0) THEN
          CQP(1)=CQP(1)*SN0CC
          DO 11 I=2,12
   11     CQP(I)=CQP(I-1)+CQP(I)*SN0CC
          HSSGCC=CQP(12)
        ENDIF
        RETURN
      ENDIF
C
C---1-LOOP CORRECTIONS
      IF (LPAR(2).EQ.1) THEN
        DFQ=1D0
        DFA=1D0
        CQU=2D0/3D0
        CQD=-1D0/3D0
        S=X*SP
        U=-X*SP-T
        CMW20=DCMPLX(MW2,-1D-8)
        CMZ20=DCMPLX(MZ2,-1D-8)
C---NORMALIZATION
        SN1CC=SN0CC
C---W SELF ENERGY
        IF (LPAR(10).GT.0) THEN
          PIW=HSSRWW(T)
          RPIW=DREAL(PIW)/(T-MW2)
          DFQ=DFQ-2D0*RPIW
          DFA=DFA-2D0*RPIW
        ENDIF
        IF ((LPAR(4).GT.1).AND.(LPAR(10).GT.0)) THEN
          DFQ=DFQ-2D0*(DELTAR-DRPIW2)
          DFA=DFA-2D0*(DELTAR-DRPIW2)
        ENDIF

C---PHOTONIC CORRECTIONS ACCORDING TO BARDIN'S PRESCRIPTION
        IF (LPAR(11).EQ.1) THEN
          DFQ=DFQ+HSCCBQ(X,Y)
          DFA=DFA+HSCCBA(X,Y)
        ENDIF
C---REMNANT OF PHOTONIC CORRECTIONS
        IF (LPAR(10).EQ.0) GOTO 300
        DFQ=DFQ+HSCCSQ(X,Y)
        DFA=DFA+HSCCSA(X,Y)
C---LEPTONIC VERTEX
        CENW=HSENUW(T)
        DELVNU=2D0*DREAL(CENW)
        DFQ=DFQ+DELVNU
        DFA=DFA+DELVNU
        IF (LPAR(12).EQ.1) THEN
          DFQ=DFQ+ALP2PI*((DLOG(MZ2/MD2)+DLOG(MZ2/ME2))/2D0+9D0/2D0)
          DFA=DFA+ALP2PI*(-(DLOG(MZ2/MU2)-DLOG(MZ2/ME2))/2D0)
C---GAMMA W BOXES (LEPTONIC PART, ONLY LOGS)
C         CWG2Q=CFWG2(T,U,CMW20)
C         CWG1A=CFWG1(T,U,CMW20)
          CWG2QL=HSWG2L(T,U,CMW20)
          CWG1AL=HSWG1L(T,U,CMW20)
          DFQ=DFQ+4D0*DREAL(CWG2QL)
          DFA=DFA-4D0*DREAL(CWG1AL)
        ENDIF
C---QUARK VERTEX
        CDUWQ=HSDUWQ(T)
        CDUWA=HSDUWA(T)
        DFQ=DFQ+2D0*DREAL(CDUWQ)
        DFA=DFA+2D0*DREAL(CDUWA)
C---QUARK PROPAGATOR RESIDUUM
        IF (LPAR(13).EQ.1) THEN
          DSQ=ALP2PI*(-DLOG(MZ2/MU2)+DLOG(MZ2/MD2))/2D0
          DFQ=DFQ+CQU*CQU*DSQ
          DFA=DFA+CQD*CQD*DSQ
        ENDIF
C---LEPTON QUARK INTERFERENCE
        IF (LPAR(14).EQ.1) THEN
          DFQ=DFQ-ALP2PI*CQU*(DLOG(MZ2/MD2)+9D0/2D0)
          DFA=DFA-ALP2PI*CQD*(DLOG(MZ2/MU2)+9D0/2D0)
C---GAMMA W BOXES (INTERFERENCE PART)
          CWG1QR=HSWG1L(T,S,CMW20)+HSWG1R(T,S,CMW20)
          CWG2QR=HSWG2L(T,U,CMW20)+HSWG2R(T,U,CMW20)
          CWG1AR=HSWG1L(T,U,CMW20)+HSWG1R(T,U,CMW20)
          CWG2AR=HSWG2L(T,S,CMW20)+HSWG2R(T,S,CMW20)
          DFQ=DFQ-4D0*CQU*(DREAL(CWG1QR)+DREAL(CWG2QR))
          DFA=DFA-4D0*CQD*(DREAL(CWG1AR)+DREAL(CWG2AR))
        ENDIF
C
C---WEAK Z-W BOXES
        IF (LPAR(15).EQ.1) THEN
C---NON-LOG TERMS FROM GAMMA-W BOXES
          CWG2QR=HSWG2R(T,U,CMW20)
          CWG1AR=HSWG1R(T,U,CMW20)
          DFQ=DFQ+4D0*DREAL(CWG2QR)
          DFA=DFA-4D0*DREAL(CWG1AR)
C---W-Z BOXES
          VEZ=VAFI(1,1,2)
          VNZ=VAFI(2,2,2)
          VUZ=VAFI(1,2,2)
          VDZ=VAFI(1,3,2)
          AEZ=-VNZ
          ANZ= VNZ
          ADZ=-VNZ
          AUZ= VNZ
          BWZ1P=ALP2PI*((VEZ+AEZ)*(VUZ+AUZ)+(VNZ+ANZ)*(VDZ+ADZ))
     *         *DREAL(HSIWZ1(T,S,CMW20,CMZ20))
          BWZ2P=ALP2PI*((VEZ+AEZ)*(VDZ+ADZ)+(VNZ+ANZ)*(VUZ+AUZ))
     *         *DREAL(HSIWZ2(T,U,CMW20,CMZ20))
          BWZ1A=ALP2PI*((VEZ+AEZ)*(VDZ+ADZ)+(VNZ+ANZ)*(VUZ+AUZ))
     *         *DREAL(HSIWZ2(T,S,CMW20,CMZ20))
          BWZ2A=ALP2PI*((VEZ+AEZ)*(VUZ+AUZ)+(VNZ+ANZ)*(VDZ+ADZ))
     *         *DREAL(HSIWZ1(T,U,CMW20,CMZ20))
          DFQ=DFQ+4D0*(T-MW2)*(BWZ1P+BWZ2P)
          DFA=DFA+4D0*(T-MW2)*(BWZ1A+BWZ2A)
        ENDIF
C---
  300   CONTINUE
        IF (LL.EQ.-1) THEN
          CQP(1) =CQP(1)  + QU*DFQ
          CQP(4) =CQP(4)  + QBD*FQA*DFA
          CQP(6) =CQP(6)  + QBS*FQA*DFA
          CQP(7) =CQP(7)  + QC*DFQ
C          CQP(10)=CQP(10) + QBB*FQA*DFA
Chs: no b-quarks
          CQP(11)=CQP(11) + QT*DFQ
        ELSEIF(LL.EQ.1) THEN
          CQP(2) =CQP(2)  + QBU*DFQ
          CQP(3) =CQP(3)  + QD*FQA*DFA
          CQP(5) =CQP(5)  + QS*FQA*DFA
          CQP(8) =CQP(8)  + QBC*DFQ
C          CQP(9) =CQP(9)  + QB*FQA*DFA
Chs: no b-quarks
          CQP(12)=CQP(12) + QBT*DFQ
        ELSE
          WRITE(LUNOUT,*) ' ERROR IN HSSGCC: WRONG LEPTON CHARGE = ',LL
          STOP
        ENDIF
C---TOTAL CROSS SECTION
        IF (LQ.GT.0) THEN
          HSSGCC=CQP(2*LQ-1)*SN1CC
        ELSEIF (LQ.LT.0) THEN
          HSSGCC=CQP(-2*LQ)*SN1CC
        ELSEIF (LQ.EQ.0) THEN
          CQP(1)=CQP(1)*SN1CC
          DO 12 I=2,12
   12     CQP(I)=CQP(I-1)+CQP(I)*SN1CC
          HSSGCC=CQP(12)
        ENDIF
      ENDIF

      END
