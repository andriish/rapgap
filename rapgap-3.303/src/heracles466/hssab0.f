C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C EPNCEMRC.S(NCVS)
C
C                EP DEEP INELASTIC SCATTERING FOR HERA
C
C                    VIRTUAL CORRECTIONS AND
C                     SOFT BREMSSTRAHLUNG
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCCCC 22. 9. 86   CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC H. SPIESBERGER CCC
C
      SUBROUTINE HSSAB0(X,Y,POL,LQ,A1,A3,B1,B3)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     BORN CROSS SECTION FOR DEEP INELASTIC ELECTRON QUARK
C     SCATTERING
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSSMCP/ VAFI(2,3,2),AFIJ(3,2,2),BFIJ(3,2,2),FLIND(2,3,2,2)
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
C
      T=-(SP-MEI2-MPRO2)*X*Y
      DG=1D0/T
      DZ=1D0/(T-MZ2)
C  STRUCTURE FUNCTIONS
C
        F1GG=FLIND(1,LQ+1,1,1)
        F1GZ=FLIND(1,LQ+1,1,2)
        F1ZZ=FLIND(1,LQ+1,2,2)
        F3GG=-FLIND(2,LQ+1,1,1)
        F3GZ=-FLIND(2,LQ+1,1,2)
        F3ZZ=-FLIND(2,LQ+1,2,2)

C...CONSTRUCTING THE CROSS SECTION
C
      A1=F1GG*DG*DG*FLIND(1,1,1,1)
     *   +2D0*F1GZ*DG*DZ*FLIND(1,1,1,2)
     *   +F1ZZ*DZ*DZ*FLIND(1,1,2,2)
C
      B3=-F3GG*DG*DG*FLIND(2,1,1,1)
     *   -2D0*F3GZ*DG*DZ*FLIND(2,1,1,2)
     *   -F3ZZ*DZ*DZ*FLIND(2,1,2,2)
C
      IF (POL.EQ.0D0) THEN
        B1=0D0
        A3=0D0
        ELSE
        B1=
     *     -2D0*F1GZ*DG*DZ*FLIND(2,1,1,2)
     *     -F1ZZ*DZ*DZ*FLIND(2,1,2,2)
C
        A3=
     *     +2D0*F3GZ*DG*DZ*FLIND(1,1,1,2)
     *     +F3ZZ*DZ*DZ*FLIND(1,1,2,2)
      ENDIF
      RETURN
      END
