CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     VERTEX CORRECTIONS
C     IR-FINITE PARTS, DOUBLE-LOG CONSTRIBUTIONS SUBTRACTED
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      FUNCTION HSFHFB(T,IVA,LF,LB,MF2)
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMPLEX*16 HSFHFB,HSCLM1,HSCLM2,HSCLM3,CMW2,CMZ2
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSSMCP/ VAFI(2,3,2),AFIJ(3,2,2),BFIJ(3,2,2),FLIND(2,3,2,2)
      COMMON /HSCBMS/ CMW2,CMZ2
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      IF ((LPAR(11).EQ.1).AND.
     I    (LPAR(12).EQ.1).OR.(LPAR(13).EQ.1)) THEN
C---PHOTON-EXCHANGE
        HSFHFB=VAFI(IVA,LF,LB)*VAFI(1,LF,1)**2 * ALP4PI*HSCLM1(T,MF2)
        ELSE
        HSFHFB=DCMPLX(0D0,0D0)
      ENDIF
      IF (LPAR(15).EQ.0) RETURN
C---Z-EXCHANGE
      IF (IVA.EQ.1) THEN
        IVAS=2
        ELSE
        IVAS=1
      ENDIF
      FCZ=VAFI(IVA,LF,LB)*(VAFI(1,LF,2)**2+VAFI(2,LF,2)**2)
     *   +VAFI(IVAS,LF,LB)*2D0*VAFI(1,LF,2)*VAFI(2,LF,2)
      HSFHFB=HSFHFB + ALP4PI*FCZ*HSCLM2(T,CMZ2)
C---W-EXCHANGE
C---W-EXCHANGE
      IF (LF.EQ.1.AND.LB.EQ.1) GOTO 10
      LFS=3
      IF (LF.EQ.3) LFS=2
      FCW=(VAFI(1,LFS,LB)+VAFI(2,LFS,LB))/4D0/SW2
      IF (LF.EQ.1.AND.LB.EQ.2) FCW=1D0/8D0/CW/SW/SW2
      HSFHFB=HSFHFB + ALP4PI*FCW*HSCLM2(T,CMW2)
C---TRIPLE GAUGE BOSON VERTEX
   10 CONTINUE
      IF (LB.EQ.1) THEN
        FC3B=3D0/4D0/SW2
        ELSE
        FC3B=-3D0/4D0/SW2*CW/SW
      ENDIF
      IF (MOD(LF,2).EQ.0) FC3B=-FC3B
      HSFHFB=HSFHFB + ALP4PI*FC3B*HSCLM3(T,CMW2)
      RETURN
      END
