C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C   SOFT BREMSSTRAHLUNG CORRECTION FOR ELECTRON ANTI-QUARK SCATTERING
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      FUNCTION HSCCSA(X,Y)
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSIRCT/ DELEPS,DELTA,EGMIN,IOPEGM
C
      HSCCSA=0D0
      CQD=-1D0/3D0
      GSP=SP-MEI2-MPRO2
      S=X*SP
      T=-GSP*X*Y
      U=-X*SP-T
      DLMWME=DLOG(MW2/ME2)
      DLMWMD=DLOG(MW2/MD2)
      DLMWMU=DLOG(MW2/MU2)
      DLQ2MW=DLOG(-T/MW2)
C
C---LEPTONIC PART
      IF (LPAR(12).EQ.1) THEN
        HSCCSA=HSCCSA+ALP2PI
     .  *(-0.5D0*DLMWME*(DLMWME+3D0)-0.5D0*DLMWMU*(DLMWMU+3D0)
     .    -DLOG(-MW2/U)*(DLOG(-ME2/U)+DLOG(-MU2/U))
     .    +DLQ2MW*(DLQ2MW-2D0*DLOG(T/U)))
      ENDIF
C---QUARKONIC PART
      IF (LPAR(13).EQ.1) THEN
        HSCCSA=HSCCSA+ALP2PI*CQD*CQD
     .  *(-0.5D0*DLMWMU*(DLMWMU+3D0)-0.5D0*DLMWMD*(DLMWMD+3D0)
     .    -DLOG(-MW2/T)*(DLOG(-MU2/T)+DLOG(-MD2/T))
     .    -2D0*DLOG(-MD2/T)+DLQ2MW*(DLQ2MW-3D0))
      ENDIF
C---LEPTON QUARK INTERFERENCE
      IF (LPAR(14).EQ.1) THEN
        HSCCSA=HSCCSA+ALP2PI*(+CQD)
     .  *(+DLOG(-U/S)*DLOG(ME2/S)+DLOG(-T/S)*DLOG(MD2/S)
     .    -DLOG(MW2*MW2/T/U)*DLOG(-MU2/U)-2D0*DLOG(-MD2/T)
     .    -DLOG(-U/S)*DLOG(U/T)-2D0*DLQ2MW*DLOG(-T/S)
     .    -DLMWMU*(DLMWMU+3D0)+DLQ2MW*(DLQ2MW-3D0+2D0*DLOG(-U/S)))
      ENDIF
      RETURN
      END
