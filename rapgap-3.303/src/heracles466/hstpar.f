C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE HSTPAR
C---
C   TEST CURRENTLY DEFINED RUN PARAMETERS
C   AGAINST THE INPUT READ FROM UNIT LUNDAT
C
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
C
      PARAMETER(TESACC=1D-10)
      COMMON /HSOPTN/ INT2(5),INT3(15),ISAM2(5),ISAM3(15),
     *                IOPLOT,IPRINT,ICUT
      COMMON /HSUNTS/ LUNTES,LUNDAT,LUNIN,LUNOUT,LUNRND
      COMMON /HSCUTS/ XMIN,XMAX,Q2MIN,Q2MAX,YMIN,YMAX,WMIN,GMIN
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      COMMON /HSSTRP/ ICODE,ILIB,ILQMOD
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSPARM/ POLARI,LLEPT,LQUA
      COMMON /HSIRCT/ DELEPS,DELTA,EGMIN,IOPEGM
      COMMON /HSISGM/ TCUTQ,TCUTQS
      DIMENSION LPARIT(12)
C
      READ(LUNDAT,ERR=9901,END=9901,IOSTAT=IOS)
     &    TXMIN,TXMAX,TQ2MIN,TYMIN,TYMAX,TWMIN,ICUTT
      READ(LUNDAT,ERR=9901,END=9901,IOSTAT=IOS) TTCUTQ,TTCTQS
      READ(LUNDAT,ERR=9901,END=9901,IOSTAT=IOS)
     &      (LPARIT(I),I=1,12)
      READ(LUNDAT,ERR=9901,END=9901,IOSTAT=IOS)
     &    ICODET,ILIBT,ILQMODT
      READ(LUNDAT,ERR=9901,END=9901,IOSTAT=IOS)
     &    TEELE,TEPRO,TPOLAR,LLEPTT
      READ(LUNDAT,ERR=9901,END=9901,IOSTAT=IOS)  TEGMIN
C
      IF((ABS(TXMIN-XMIN).GT.TESACC)
     &    .OR. (ABS(TXMIN-XMIN).GT.TESACC)
     &    .OR. (ABS(TXMAX-XMAX).GT.TESACC)
     &    .OR. (ABS(TQ2MIN-Q2MIN).GT.TESACC)
     &    .OR. (ABS(TYMIN-YMIN).GT.TESACC)
     &    .OR. (ABS(TYMAX-YMAX).GT.TESACC)
     &    .OR. (ICUTT.NE.ICUT))                 THEN
        WRITE(LUNOUT,'(/2A,I3//A/)')
     &      ' CURRENT KINEMATICAL CUTS INCONSISTENT WITH THE DATA SET',
     &      ' TO BE READ FROM UNIT',LUNDAT,
     &      ' CUTS DEFINED IN THE DATA SET:'
        WRITE(LUNOUT,'(10X,A,1PE10.3,5X,A,1PE10.3)')
     *     ' XMIN=',TXMIN,' XMAX=',TXMAX
        WRITE(LUNOUT,'(10X,A,1PE10.3)')
     *     ' Q2MIN=',TQ2MIN
        WRITE(LUNOUT,'(10X,A,1PE10.3,26X,A)')
     *     ' WMIN=', TWMIN, ' (ACTIVE ONLY FOR ICUT>1)'
        WRITE(LUNOUT,'(10X,A,1PE10.3,5X,A,1PE10.3,5X,A)')
     *     ' YMIN=',TYMIN,' YMAX=',TYMAX, ' (ACTIVE ONLY FOR ICUT=3)'
        WRITE(LUNOUT,'(10X,A,I2)')
     *     ' ICUT=',ICUTT
C
        WRITE(LUNOUT,'(/A/)')  ' CURRENTLY DEFINED PARAMETERS:'
        WRITE(LUNOUT,'(10X,A,1PE10.3,5X,A,1PE10.3)')
     *     ' XMIN=',XMIN,' XMAX=',XMAX
        WRITE(LUNOUT,'(10X,A,1PE10.3)')
     *     ' Q2MIN=',Q2MIN
        WRITE(LUNOUT,'(10X,A,1PE10.3,26X,A)')
     *     ' WMIN=', WMIN, ' (ACTIVE ONLY FOR ICUT>1)'
        WRITE(LUNOUT,'(10X,A,1PE10.3,5X,A,1PE10.3,5X,A)')
     *     ' YMIN=',YMIN,' YMAX=',YMAX, ' (ACTIVE ONLY FOR ICUT=3)'
        WRITE(LUNOUT,'(10X,A,I2)')
     *     ' ICUT=',ICUT
        WRITE(LUNOUT,100)
        STOP
      ENDIF
C
      IF(INT3(4).GT.1 .OR. ISAM3(4).GE.1) THEN
        IF(ABS(TTCUTQ-TCUTQ).GT.TESACC
     &      .OR. ABS(TTCTQS-TCUTQS).GT.TESACC) THEN
          WRITE(LUNOUT,'(/2A,I3//A/)')
     &      ' CURRENT ANGULAR CUTS FOR QUARKONIC BREMSSTRAHLUNG',
     &      ' INCONSISTENT WITH THE DATA SET TO BE READ FROM UNIT',
     &        LUNDAT,
     &      ' CUTS DEFINED IN THE DATA SET:'
          WRITE(LUNOUT,'(10X,A,1PE10.3,5X,A,1PE10.3)')
     &     ' TCUTQ=',TTCUTQ,' TCUTQS=',TTCTQS
          WRITE(LUNOUT,'(/A/)')  ' CURRENTLY DEFINED PARAMETERS:'
          WRITE(LUNOUT,'(10X,A,1PE10.3,5X,A,1PE10.3)')
     &     ' TCUTQ=',TCUTQ,' TCUTQS=',TCUTQS
          WRITE(LUNOUT,100)
          STOP
        ENDIF
      ENDIF
C
      DO 11 I=1,12
        IF(LPARIN(I).NE.LPARIT(I)) GOTO 12
  11  CONTINUE
      GOTO 13
  12  CONTINUE
      WRITE(LUNOUT,'(//2A,I3//A,I3,A)')
     &  ' CURRENT GSW-PARAMETERS INCONSISTENT WITH DATA SET ',
     &  ' TO BE READ FROM UNIT ',LUNDAT,
     &  ' PARAMETERS FROM DATA SET ON UNIT ',LUNDAT, ' :'
      WRITE(LUNOUT,'(12I3)') LPARIT
      WRITE(LUNOUT,'(/A/)')  ' CURRENTLY DEFINED PARAMETERS:'
      WRITE(LUNOUT,'(12I3)') LPARIN
      WRITE(LUNOUT,100)
      STOP
C
  13  CONTINUE
C
      IF(ICODET.NE.ICODE
     &  .OR.ILIBT.NE.ILIB
     &  .OR.ILQMODT.NE.ILQMOD) THEN
        WRITE(LUNOUT,'(//2A,I3//A,I3,A)')
     &    ' OTHER PARTON DISTRIBUTIONS USED FOR THE DATA SET ',
     &    ' TO BE READ FROM UNIT',LUNDAT,
     &    ' PARAMETER FROM DATA SET ON UNIT',LUNDAT,' : '
        WRITE(LUNOUT,'(10X,A,I7,A,I3,A,I3)')
     &      ' ICODE = ',ICODET,' ILIB = ',ILIBT,' ILQMOD = ',ILQMODT
        WRITE(LUNOUT,'(/A)')
     &    ' CURRENTLY DEFINED PARAMETERS:'
        WRITE(LUNOUT,'(10X,A,I7,A,I3,A,I3)')
     &      ' ICODE = ',ICODE,' ILIB = ',ILIB,' ILQMOD = ',ILQMOD
C
        WRITE(LUNOUT,100)
        STOP
      ENDIF
C
      IF((ABS(TEELE-EELE).GT.TESACC)
     &    .OR. (ABS(TEPRO-EPRO).GT.TESACC)
     &    .OR. (ABS(TPOLAR-POLARI).GT.TESACC)
     &    .OR. (LLEPTT.NE.LLEPT))                 THEN
        WRITE(LUNOUT,'(/2A,I3//A,I3/)')
     &      ' CURRENT BEAM PARAMETERS INCONSISTENT WITH THE DATA SET',
     &      ' TO BE READ FROM UNIT',LUNDAT,
     &      ' PARAMETERS FROM DSTS SET ON UNIT',LUNDAT,' :'
        WRITE(LUNOUT,'(10X,A,F8.1,A)')
     &      ' ENERGY OF INCIDENT ELECTRON =',TEELE,' GEV'
        WRITE(LUNOUT,'(10X,A,I3)')
     &                  ' CHARGE OF INCIDENT ELECTRON =',LLEPTT
        WRITE(LUNOUT,'(10X,A,F8.4)')
     &      ' DEGREE OF BEAM POLARIZATION =', TPOLAR
        WRITE(LUNOUT,'(10X,A,F8.1,A)')
     &      ' ENERGY OF INCIDENT PROTON =',TEPRO,' GEV'
C
        WRITE(LUNOUT,'(/A/)')  ' CURRENTLY DEFINED PARAMETERS:'
        WRITE(LUNOUT,'(10X,A,F8.1,A)')
     &      ' ENERGY OF INCIDENT ELECTRON =',EELE,' GEV'
        WRITE(LUNOUT,'(10X,A,I3)')
     &                  ' CHARGE OF INCIDENT ELECTRON =',LLEPT
        WRITE(LUNOUT,'(10X,A,F8.4)')
     &      ' DEGREE OF BEAM POLARIZATION =', POLARI
        WRITE(LUNOUT,'(10X,A,F8.1,A)')
     *      ' ENERGY OF INCIDENT PROTON =',EPRO,' GEV'
C
        WRITE(LUNOUT,100)
        STOP
      ENDIF
C
      IF(ABS(TEGMIN-EGMIN).GT.TESACC) THEN
        WRITE(LUNOUT,'(//2A,I3//A,I3,A,1PE11.3,A)')
     &      ' CURRENT CUT ON PHOTON ENERGY INCONSISTENT WITH DATA SET ',
     &       ' TO BE READ FROM UNIT',LUNDAT,
     &       ' CUT FROM DATA SET ON UNIT',LUNDAT,' :  EGMIN=',
     &       TEGMIN,' GEV'
        WRITE(LUNOUT,'(/A,1PE11.3,A)')
     &       ' CURRENTLY DEFINED PARAMETER: EGMIN=',EGMIN,' GEV'
        WRITE(LUNOUT,100)
        STOP
      ENDIF
C
      RETURN
C
 9901 CONTINUE
      WRITE(LUNOUT,'(/A,I3/A,I3/A)')
     &    ' ***  ERROR IN HSTPAR READING DATA FROM UNIT ',LUNDAT,
     &    ' ***  IOS=',IOS,
     &    ' ***  EXECUTION STOPPED'
      WRITE(LUNOUT,'(/A/5X,A/5X,6(1PE12.4),I3/5X,A,I7,A,I3,A,I3/
     &               5X,A/5X,3(1PE12.4),I3/5X,A,1PE12.4)')
     &    ' ***  ACTUAL VALUES OF PARAMETERS TO BE READ:',
     &    ' TXMIN,TXMAX,TQ2MIN,TYMIN,TYMAX,TWMIN,ICUTT',
     &      TXMIN,TXMAX,TQ2MIN,TYMIN,TYMAX,TWMIN,ICUTT,
     &    ' ICODET=',ICODET,' ILIBT=', ILIBT,' ILQMODT=',ILQMODT,
     &    ' TEELE,TEPRO,TPOLAR,LLEPTT',
     &      TEELE,TEPRO,TPOLAR,LLEPTT,
     &    ' TEGMIN=', TEGMIN
      STOP
C
 100  FORMAT(/' ***  EXECUTION STOPPED IN SUBROUTINE HSTPAR')
      END
