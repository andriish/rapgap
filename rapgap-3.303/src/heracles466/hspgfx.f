
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

      FUNCTION HSPGFX()
C
C   THIS PROGRAM DETERMINES MW FOR GIVEN MZ VIA MU-LIFETIME
C   ( W. HOLLIK, HAMBURG )
C   ( CHANGED AD 6.8.88)
C   ( CHANGED HS 14.5.91)
C
C   INPUT:   Z0 MASS = MZ, HIGGS MASS = MH, TOP MASS= MT
C   OUTPUT:  W MASS = MW
C   OUTPUT ON COMMONS: DELTA-R = DELTAR, WEAK MIXING ANGLE =SW2
C
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMPLEX*16 HSSRWW
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      COMMON /HSDELR/ DELTAR,AGF0,DRHOT,DALPMZ,XGMT,ALPQCD,BTOP4,DRPIW2
      COMMON /HSUNTS/ LUNTES,LUNDAT,LUNIN,LUNOUT,LUNRND
      DIMENSION DR2(20)

C
      I=1
C---SW2 = SIN**2 THETA-W, START WITH APPROXIMATE VALUE
      SQ=DSQRT(1D0-4D0*AGF0/MZ2/(1D0-.07D0))
      SW2=(1D0-SQ)/2D0
      DR2(1)=0.07D0
      CW2=1D0-SW2
      SW=DSQRT(SW2)
      CW=DSQRT(CW2)
      MW=MZ*CW
      MW2=MW*MW

C---DELTAR MEANS THE QUANTITY 'DELTA R' IN MU LIFETIME FORMULA
51    CONTINUE
      LPR15K=LPAR(15)
      LPR7K=LPAR(7)
      LPAR(15)=1
      LPAR(7)=2
      DELTAR=DREAL(HSSRWW(0D0))/MW2
     *    +ALP4PI/SW2*(6D0+(3.5D0/SW2-2D0)*DLOG(CW2))
      LPAR(15)=LPR15K
      LPAR(7)=LPR7K
C---LEADING TWO-LOOP TERMS
      DRHOT=3D0*ALP4PI/4D0/SW2/CW2*MT2/MZ2
      DALPMZ=0.0602D0
      XGMT=GF*MT2/SQRT(2D0)/8D0/PI/PI
      ALAMB2=0.14D0**2D0
      NF=5
      ALPQCD=0D0
      DLRQCD=0D0
      IF (LPAR(3).GE.2) THEN
        ALPQCD=4D0*PI/(11D0-2D0/3D0*NF)/DLOG(    MT2/ALAMB2)
        DLRQCD=CW2/SW2*2D0*ALPQCD/PI*(PI*PI/3D0+1D0)*XGMT
      ENDIF
      DELTR2=0D0
      IF (LPAR(3).GE.1) THEN
        DELTR2=CW2/SW2*(CW2/SW2*DRHOT*DRHOT/(1D0-DALPMZ)
     *                  -3D0*XGMT*XGMT*(19D0-2D0*PI*PI) )
      ENDIF
      DELTAR=DELTAR+DELTR2+DLRQCD
      DRPIW2=DELTR2+DLRQCD
      SQ=DSQRT(1D0-4D0*AGF0/MZ2/(1D0-DELTAR))

C---THE CORRECTED VALUE FOR SIN**2 THETA-W
      SW2=(1D0-SQ)/2D0
      DR2(I+1)=DELTAR
      DS2=DABS(DR2(I+1)-DR2(I))
      CW2=1.D0-SW2
      CW=DSQRT(CW2)
      SW=DSQRT(SW2)
C---THE CORRECTED VALUE FOR THE W MASS
      MW=MZ*CW
      MW2=MW*MW
C---DELTA-RHO
      BTOP4=1D0/(1D0+DRHOT)/(1D0-3D0*XGMT*(1D0+XGMT*(19D0-2D0*PI*PI)))
      BTOP4=SQRT(BTOP4)

      IF(DS2.LT.1D-8) THEN
         HSPGFX=MW
         RETURN
      END IF
      I=I+1
      IF(I.LE.20) GOTO  51
      HSPGFX=MW
      WRITE(LUNOUT,1)
    1 FORMAT(' WARNING: CALCULATION OF MW IN HSPGFX DID NOT CONVERGE',/
     F      ,' AFTER 20 ITERATIONS. CHECK INPUT MASSES',/)
      RETURN
      END
