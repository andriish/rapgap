C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C   ELASTIC FORM FACTORS FOR THE PROTON
C   TAKEN FROM STEIN ET AL. (PRD 12 (1975) 1884)
C   INCLUDING VIRTUAL&SOFT CORRECTIONS AND VACUUM POLARIZATION
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      SUBROUTINE HSFIEL(Q2,F1EL,F2EL)
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMPLEX*16 HSSRGG,CG
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSELEP/ IDIPOL
      DIMENSION HDIP(0:5)
      DATA AK /2.7927D0/
      DATA Q20 /0.71D0/
      DATA HDIP /1.0007D0, 1.01807D0, 1.05584D0,
     *           0.836380D0, 0.6864584D0, 0.672830D0/

      TAU=Q2/4D0/MPRO2
      GEP=1D0/(1D0+Q2/Q20)**2
      PDEV=1D0
      IF (IDIPOL.EQ.1) THEN
C...DEVIATION FROM DIPOLE FORM FACTOR
        Q=DSQRT(Q2)
        PDEV=0D0
        DO 1 I=0,5
        PDI=1D0
        DO 2 J=0,5
        IF (J.EQ.I) GOTO 2
        PDI=PDI*(Q-DFLOAT(J))/DFLOAT(I-J)
    2   CONTINUE
        PDEV=PDEV+PDI*HDIP(I)
    1   CONTINUE
      ENDIF
      GEP=GEP*PDEV
      GEM=GEP*AK
C...STRUCTURE FUNCTIONS W1EL, W2EL -> F1,F2
      W1EL=TAU*GEM*GEM
      W2EL=(GEP*GEP+TAU*GEM*GEM)/(1D0+TAU)
      F1EL=W1EL/(2D0*TAU)
      F2EL=W2EL
C...INCLUDE VIRTUAL AND SOFT PHOTON CORRECTIONS
      IF (LPAR(2).GT.0) THEN
      SHAT=SP-MEI2-MPRO2
      X=1D0
      Y=Q2/SHAT
C..PHOTON SELF ENERGY
      IF (LPAR(7).GE.1) THEN
        T=-Q2
        CG=HSSRGG(T)
        PIGGG=DREAL(CG)/T
        ELSE
        PIGGG=0D0
      ENDIF
      DVACGG=1D0/(1D0+PIGGG)
C..LEPTONIC QED VERTEX CORRECTIONS INCLUDING SOFT BREMSSTRAHLUNG
      IF (LPAR(12).GE.1) THEN
        DVRTXV=HSDQDV(X,Q2)*ALP2PI
        DVRTXS=HSDQDS(X,Q2)*ALP2PI
        DVRTX1=DVRTXV-(Q2+4D0*MEI2)/(Q2-2D0*MEI2)*DVRTXS
        DVRTX2=DVRTXV+
     *     (2D0*(1D0-Y)+Y*Y/2D0)/(1D0-Y-MPRO2*Q2/SHAT/SHAT)*DVRTXS
        ELSE
        DVRTX1=0D0
        DVRTX2=0D0
      ENDIF
      IF (LPAR(3).LT.3) THEN
        F1EL=F1EL*(DVRTX1+DVACGG*DVACGG)
        F2EL=F2EL*(DVRTX2+DVACGG*DVACGG)
        ELSE
        F1EL=F1EL*(1D0+DVRTX1)*DVACGG*DVACGG
        F2EL=F2EL*(1D0+DVRTX2)*DVACGG*DVACGG
      ENDIF
      ENDIF
      RETURN
      END
