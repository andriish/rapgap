C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C   INTEGRAND OF KQ TERM (QUARKONIC RADIATION)
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      FUNCTION HSK1K3(X)
C
C  X(1) -->  XX
C  X(2) -->  G=-1/Q**2   -->  Q**2=-1/G-->  Y
C  X(3) -->  LOG(XS-XX)
C  X(4) -->  LOG(2*K.Q)
C  X(5) -->  2*K.P
C
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSOPTN/ INT2(5),INT3(15),ISAM2(5),ISAM3(15),
     *                IOPLOT,IPRINT,ICUT
      COMMON /HSCUTS/ XMIN,XMAX,Q2MIN,Q2MAX,YMIN,YMAX,WMIN,GMIN
      COMMON /HSTCUT/ THEMIN,CTHMIN,CTHCON
      COMMON /HSPCUT/ PTMIN,PTXM0
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
      COMMON /HSUNTS/ LUNTES,LUNDAT,LUNIN,LUNOUT,LUNRND
      COMMON /HSCUMS/ CQP(12)
      COMMON /HSKPXY/ XX,Y
      COMMON /HSSMCP/ VAFI(2,3,2),AFIJ(3,2,2),BFIJ(3,2,2),FLIND(2,3,2,2)
      COMMON /HSXSLM/ XSMIN,XSCUT
      COMMON /HSCMSP/ EQ,PQ,EEL,PEL,ES,PS,COSE,SINE,OMEGA
      COMMON /HSLABP/ EH,PH,EQH,PQH,ESH,PSH,COSEH,SINEH
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSIRCT/ DELEPS,DELTA,EGMIN,IOPEGM
      COMMON /HSIKP/  S,T,U,SS,TS,US,DKP,DKPS,DKQ,DKQS
      COMMON /HSPDFQ/ QU,QBU,QD,QBD,QS,QBS,QC,QBC,QB,QBB,QT,QBT
      COMMON /HSPARM/ POLARI,LLEPT,LQUA
      COMMON /HSGIKP/ GS,GU,GX,TP,UP
      COMMON /HSFIJK/ F1(2,2),F2(2,2),F3(2,2)
      COMMON /HSPSPC/ IPHSPC
      COMMON /HSPDFO/ IPDFOP,IFLOPT,LQCD,LTM,LHT
      COMMON /HSWGTC/ IWEIGS
      COMMON /HSISGM/ TCUTQ,TCUTQS
      DIMENSION X(5),DBOS(2),DSBOS(2)


C---X-VALUE
      XX=XMIN + (XMAX-XMIN)*X(1)
C---Y-VALUE
      GS=SP-MEI2-MPRO2
      YMAXX=XX*(1D0-4D0*MEI2*MPRO2/GS/GS)/(XX*(1D0+XX*MPRO2/GS)+MEI2/GS)
      IF(ICUT.LT.3) THEN
C---CUT IN EXTERNALLY DEFINED Q**2(MIN)
        GMAX=-1D0/(XX*YMAXX*GS)
        ELSEIF(ICUT.EQ.3) THEN
C---CUT IN Y
        QQ2MIN=XX*YMIN*GS
C---CUT ON ELECTRON SCATTERING ANGLE (MASSES NEGLECTED)
        YMINTH=1D0/(1D0+XX*CTHCON)
        QT2MIN=XX*YMINTH*GS
C---CUT ON ELECTRON TRANSVERSE MOMENTUM (MASSES NEGLECTED)
        QP2MIN=XX*GS/2D0*(1D0-DSQRT(1D0-PTXM0/XX))
        QP2MAX=XX*GS/2D0*(1D0+DSQRT(1D0-PTXM0/XX))
        YP2MAX=QP2MAX/GS/XX
        GMIN=-1D0/MAX(Q2MIN,QQ2MIN,QT2MIN,QP2MIN)
        GMAX=-1D0/(XX*MIN(YMAX,YMAXX,YP2MAX)*GS)
        GMAX=MIN(GMAX,-1D0/Q2MAX)
        ELSE
        WRITE(LUNOUT,'(/A,I5/A)') ' WRONG VALUE OF ICUT:',ICUT,
     *                            ' STOP IN HSK1K3'
        STOP
      ENDIF
      DG2=DMAX1(GMAX-GMIN,0D0)
      GACT=GMIN+X(2)*DG2
      Y=-1D0/(XX*GACT*GS)
C
C---EXTERNAL WEIGHT
      IACPT=1
      IF (IWEIGS.GT.0) THEN
        CALL HSWGTX(XX,Y,IACPT)
        IF (IACPT.EQ.0) THEN
          HSK1K3=0D0
          RETURN
        ENDIF
      ENDIF
C
      CALL HSFIVC(XX,Y)
      CALL HSDELO(XX,Y)
      XSMIN = HSXSMN(XX,Y)
C     XSCUT = HSXSCT(XX,Y)

      XSMAX = 1D0
      XSMINI = XSMIN
      IF((XSMAX-XSMINI).LE.1D-14) THEN
        HSK1K3=0D0
        RETURN
      ENDIF

C---SUBSTITUTION UV=LN(XS-X)
      UVMIN = DLOG(XSMINI-XX)
      UVMAX = DLOG(XSMAX-XX)
      UV = UVMIN + (UVMAX-UVMIN)*X(3)
      XS = XX+DEXP(UV)
      CALL HSFCMS(XX,Y,XS)
      IF(IPHSPC.EQ.1) THEN
        HSK1K3=0D0
        RETURN
      ENDIF
      CALL HSFLAB(XX,Y,XS)
      CALL HSLZK3(ZMIN,ZMAX)
      IF(IPHSPC.EQ.1) THEN
        HSK1K3=0D0
        RETURN
      ENDIF

C---SUBSTITUTION V = LN(A3)
      A3MAX = 2D0*OMEGA*(EQ-PQ*ZMIN)
      IF(ZMAX.GE.0.99D0) THEN
        A3MIN = MQI2*OMEGA/EQ
      ELSE
        A3MIN = 2D0*OMEGA*(EQ-PQ*ZMAX)
      ENDIF

C---CUT FROM EXCLUSION OF INITIAL STATE COLLINEAR PHOTONS
      IF(TCUTQ.NE.0D0)A3MIN=MAX(A3MIN,2D0*DELTA*(EQH-PQH*DCOS(TCUTQ)))
C---CUT IN HADRONIC MASS
      IF (ICUT.GT.1) THEN
        W2CUT=WMIN*WMIN
        AWCUT=XS*Y*GS-XS*(W2CUT-MPRO2)/(1D0-XS)
        A3MAX=MIN(A3MAX,AWCUT)
      ENDIF

      IF (A3MAX.LE.0D0.OR.A3MIN.LE.0D0) THEN
        HSK1K3=0D0
        RETURN
      ENDIF
      VMAX = DLOG(A3MAX)
      VMIN = DLOG(A3MIN)
      V= VMIN + (VMAX-VMIN)*X(4)
      A3 = DEXP(V)

C---NO SUBSTITUTION FOR A1
      CALL HSL1K3(A3,XX,Y,XS,A1MIN,A1MAX,A1M,A1P,CFKP)
      IF(IPHSPC.EQ.1) THEN
        HSK1K3=0D0
        RETURN
      ENDIF
      A1=A1MIN + (A1MAX-A1MIN)*X(5)
      SQGRM2=-CFKP*(A1-A1M)*(A1-A1P)
      SQGRAM = DSQRT(DABS(SQGRM2))
      CALL HSFIV3(XX,Y,XS,A1,A3)
      CALL HSPVER(XS,-T)

      OMH = (PH*DKQ+PQH*DKP)/(PH*EQH+PQH*EH)
C---SUBTRACTION OF INITIAL STATE MASS SINGULARITY
      DCKQH = (DKQ/OMH-EQH+PQH)/PQH
      CKQH = 1D0-DCKQH
      IF(DABS(CKQH).GT.1D0)CKQH=DSIGN(1D0,CKQH)
      THKQ = DACOS(CKQH)
      IF(THKQ.LT.TCUTQ)THEN
        HSK1K3=0D0
        RETURN
      ENDIF
C---SUBTRACTION OF FINAL STATE MASS SINGULARITY
      EQSH = EH+EQH-OMH-ESH
      PQSH = DSQRT(DABS((EQSH+MQF)*(EQSH-MQF)))
      DCKQSH = (DKQS/OMH-EQSH+PQSH)/PQSH
      CKQSH = 1D0-DCKQSH
      IF(DABS(CKQSH).GT.1D0)CKQSH=DSIGN(1D0,CKQSH)
      THKQS = DACOS(CKQSH)
      IF(THKQS.LT.TCUTQS)THEN
        HSK1K3=0D0
        RETURN
      ENDIF

      A2=2D0*DKPS
      DBOS(1)=1D0/T
      DBOS(2)=1D0/(T-MZ2)
      DSBOS(1)=1D0/TS
      DSBOS(2)=1D0/(TS-MZ2)
      SU1 = S*S+SS*SS+U*U+US*US
      SU2 = (S-U)*(S+U) + (SS-US)*(SS+US)
      SU3 = (S*S+U*U)/2D0
      SU4 = (S-U)*(S+U)/2D0
      SU5 = (SS*SS+US*US)/2D0
      SU6 = (SS-US)*(SS+US)/2D0
      PROP1= -T/DKQ/DKQS
      PROP2= -4D0*MQF2/DKQS/DKQS
      PROP3= -4D0*MQI2/DKQ/DKQ
      IF(LPAR(14).EQ.0)THEN
        PROPI1=0D0
      ELSE
        PROPI1= - LLEPT
     &       * (SS/DKPS/DKQS + S/DKP/DKQ + U/DKPS/DKQ + US/DKP/DKQS)
      ENDIF
      DO 1 IFL=1,12
        CQP(IFL) = 0D0
 1    CONTINUE
      FUP = 0D0
      FDO = 0D0
      FUPB =0D0
      FDOB =0D0
      FUPI = 0D0
      FDOI = 0D0
      FUPIB =0D0
      FDOIB =0D0
      IF(LPAR(14).NE.0)THEN
          RALEP = 8D0*XS*(
     &     GX/2D0*(
     &     +(T+4D0*MEI2)*(1D0/A2/TS/TS-1D0/A1/TS/TS)
     &     +2D0/TS/TS - 1D0/A1/TS + 1D0/A2/TS + 2D0/A1/A2
     &     -8D0*MEI2*MEI2/A1/A2/TS/TS
     &     +4D0*MEI2*MEI2/TS/TS*(1D0/A1/A1+1D0/A2/A2)
     &     +2D0*MEI2/TS*(1D0/A1/A1+1D0/A2/A2)  )
     &   +XS*(
     &    -(GS*GS + GU*GU - GX*(GS+GU) -4D0*MEI2*MPRO2)/TS/A1/A2
     &    +(-GS*GX + MPRO2*T)/A1/TS/TS
     &    +(GU*GX - MPRO2*T)/A2/TS/TS
     &    +(-2D0*GS*GU + GX*(GS+GU-GX))*2D0*MEI2/A1/A2/TS/TS
     &    +GU*(GX-GU)*2D0*MEI2/A1/A1/TS/TS
     &    +GS*(GX-GS)*2D0*MEI2/A2/A2/TS/TS
     &    -2D0*MEI2*MPRO2/TS*(1D0/A1/A1+1D0/A2/A2)
     &    -2D0*MPRO2*(1D0/TS/TS+1D0/A1/A2)
     &    +MPRO2/TS*(1D0/A1-1D0/A2) ))
          RBLEP = 8D0*XS*(
     &    -DFLOAT(LLEPT)*(
     &     (GS-GU)/A1/A2
     &     -2D0*MEI2*(GS-GU)/A1/A2/TS
     &     +(GX/2D0-GS)/A1/TS
     &     +(GX/2D0-GU)/A2/TS
     &     +GX*T/2D0/A1/TS/TS
     &     +GX*T/2D0/A2/TS/TS
     &     +(GX-2D0*GU)*MEI2/A1/A1/TS
     &     +(2D0*GS-GX)*MEI2/A2/A2/TS))
           PLEP=0D0
           PHAD=0D0
           PFRCH=0D0
      DO 51 IB1=1,2
       DO 51 IB2=1,2
         PLEP=(AFIJ(2,IB1,IB2)*RALEP+BFIJ(2,IB1,IB2)*RBLEP)
     &                *DSBOS(IB1)*DSBOS(IB2)*TS*TS  + PLEP
         PHAD =((AFIJ(2,IB1,IB2)*SU1 - BFIJ(2,IB1,IB2)*SU2*LLEPT)*PROP1
     &         +(AFIJ(2,IB1,IB2)*SU3 - BFIJ(2,IB1,IB2)*SU4*LLEPT)*PROP2
     &         +(AFIJ(2,IB1,IB2)*SU5 - BFIJ(2,IB1,IB2)*SU6*LLEPT)*PROP3)
     &         *DBOS(IB1)*DBOS(IB2)*4D0/9D0 +PHAD
 51      CONTINUE
          SUTOT=PHAD+PLEP
          FRCH=PHAD/SUTOT
      ELSE
        FRCH=0D0
      ENDIF
      DO 10 IB1 = 1,2
       DO 20 IB2 = 1,2
        PROP = DBOS(IB1)*DBOS(IB2)
        AB12UP = AFIJ(2,IB1,IB2)*SU1 - BFIJ(2,IB1,IB2)*SU2*LLEPT
        AB12UM = AFIJ(2,IB1,IB2)*SU1 + BFIJ(2,IB1,IB2)*SU2*LLEPT
        AB12DP = AFIJ(3,IB1,IB2)*SU1 - BFIJ(3,IB1,IB2)*SU2*LLEPT
        AB12DM = AFIJ(3,IB1,IB2)*SU1 + BFIJ(3,IB1,IB2)*SU2*LLEPT
        AB34UP = AFIJ(2,IB1,IB2)*SU3 - BFIJ(2,IB1,IB2)*SU4*LLEPT
        AB34UM = AFIJ(2,IB1,IB2)*SU3 + BFIJ(2,IB1,IB2)*SU4*LLEPT
        AB34DP = AFIJ(3,IB1,IB2)*SU3 - BFIJ(3,IB1,IB2)*SU4*LLEPT
        AB34DM = AFIJ(3,IB1,IB2)*SU3 + BFIJ(3,IB1,IB2)*SU4*LLEPT
        AB56UP = AFIJ(2,IB1,IB2)*SU5 - BFIJ(2,IB1,IB2)*SU6*LLEPT
        AB56UM = AFIJ(2,IB1,IB2)*SU5 + BFIJ(2,IB1,IB2)*SU6*LLEPT
        AB56DP = AFIJ(3,IB1,IB2)*SU5 - BFIJ(3,IB1,IB2)*SU6*LLEPT
        AB56DM = AFIJ(3,IB1,IB2)*SU5 + BFIJ(3,IB1,IB2)*SU6*LLEPT
       IF(LPAR(13).NE.0)THEN
        FUP = (AB12UP*PROP1+AB34UP*PROP2+AB56UP*PROP3) * PROP  + FUP
        FDO = (AB12DP*PROP1+AB34DP*PROP2+AB56DP*PROP3) * PROP  + FDO
        FUPB =(AB12UM*PROP1+AB34UM*PROP2+AB56UM*PROP3) * PROP  + FUPB
        FDOB =(AB12DM*PROP1+AB34DM*PROP2+AB56DM*PROP3) * PROP  + FDOB
       ENDIF
        IF(LPAR(14).NE.0)THEN
          PROPI2 = DBOS(IB1)*DSBOS(IB2)
          FUPI = AB12UP*PROPI1 * PROPI2                   + FUPI
          FDOI = AB12DP*PROPI1 * PROPI2                   + FDOI
          FUPIB =AB12UM*PROPI1 * PROPI2                   + FUPIB
          FDOIB =AB12DM*PROPI1 * PROPI2                   + FDOIB
        ENDIF
20     CONTINUE
10    CONTINUE

      CQP(1) =  QU *(FUP  *4D0/9D0 + FRCH*FUPI  *  2D0/3D0 )
      CQP(2) =  QBU*(FUPB *4D0/9D0 + FRCH*FUPIB *(-2D0/3D0)) + CQP(1)
      CQP(3) =  QD *(FDO  *1D0/9D0 + FRCH*FDOI  *(-1D0/3D0)) + CQP(2)
      CQP(4) =  QBD*(FDOB *1D0/9D0 + FRCH*FDOIB *  1D0/3D0 ) + CQP(3)
      CQP(5) =  QS *(FDO  *1D0/9D0 + FRCH*FDOI  *(-1D0/3D0)) + CQP(4)
      CQP(6) =  QBS*(FDOB *1D0/9D0 + FRCH*FDOIB *  1D0/3D0 ) + CQP(5)
      CQP(7) =  QC *(FUP  *4D0/9D0 + FRCH*FUPI  *  2D0/3D0 ) + CQP(6)
      CQP(8) =  QBC*(FUPB *4D0/9D0 + FRCH*FUPIB *(-2D0/3D0)) + CQP(7)
      CQP(9) =  QB *(FDO  *1D0/9D0 + FRCH*FDOI  *(-1D0/3D0)) + CQP(8)
      CQP(10) =  QBB*(FDOB *1D0/9D0 + FRCH*FDOIB*  1D0/3D0 ) + CQP(9)
      CQP(11) =  QT *(FUP  *4D0/9D0 + FRCH*FUPI *  2D0/3D0 ) + CQP(10)
      CQP(12) =  QBT*(FUPB *4D0/9D0 + FRCH*FUPIB*(-2D0/3D0)) + CQP(11)
      SUMME = CQP(12)
      HSK1K3 = SUMME*Y*SX1NRM/4D0/SQGRAM/XS
     *      * (UVMAX-UVMIN)*(XS-XX)*(VMAX-VMIN)*A3*(A1MAX-A1MIN)
     *      * (XMAX-XMIN) * DG2/(XX*GS*GACT**2)
      RETURN
      END
