C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C     SOFT BREMSSTRAHLUNG FOR ELECTRON-QUARK SCATTERING
C     NO RESTRICTION ON Q2, NO QUARKONIC AND NO INTERFERENCE PARTS
C
      FUNCTION HSDQDV(X,Q2)
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMPLEX*16 HSSPEN,HSFONE,Z1,Z2,Z3,Z4
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSIRCT/ DELEPS,DELTA,EGMIN,IOPEGM
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSPARL/ LPAR(20),LPARIN(12)
C
      SHAT=SP-MPRO2-MEI2
      DELTA2=DELTA*DELTA
      EEI=EELE
      PEI=PELE
      EEI2=EEI*EEI
      AMFEL=SHAT/2D0-Q2/2D0/X
      BMFEL=Q2/2D0+MEI2
      EEF=(BMFEL*PPRO+AMFEL*PELE)/(PELE*EPRO+PPRO*EELE)
      PEF=SQRT((EEF-MEF)*(EEF+MEF))
      EEF2=EEF*EEF
      Y=Q2/SHAT/X
C
      HSDQDV=0D0
      IF (LPAR(12).EQ.1) THEN
        TAU=DSQRT(Q2*(Q2+4*MEI2))
        BETA=(TAU+Q2)*(TAU+Q2)/4D0/MEI2/Q2
        VAU=(TAU+(BETA+1D0)*Q2)/2D0/(BETA*EEI-EEF)
        Z1=DCMPLX(TAU*(TAU+Q2)/2D0/Q2/MEI2,1D-6)
        Z2=DCMPLX(2D0*TAU/(TAU+Q2),-1D-6)
        DC0=2D0*(Q2+2D0*MEI2)/TAU*DREAL(HSSPEN(Z1)-HSSPEN(Z2))
     *   +2D0-3*DREAL(HSFONE(-Q2,MEI,MEI))
        Z1=DCMPLX((EEI+PEI)*VAU/BETA/MEI2,1D-6)
        Z2=DCMPLX(VAU/(EEI+PEI)/BETA,-1D-6)
        Z3=DCMPLX((EEF+PEF)*VAU/MEI2,1D-6)
        Z4=DCMPLX(VAU/(EEF+PEF),-1D-6)
        DR1=DREAL(HSSPEN(Z1))
        DR2=DREAL(HSSPEN(Z2))
        DR3=DREAL(HSSPEN(Z3))
        DR4=DREAL(HSSPEN(Z4))
        A1S=DABS(VAU*(EEI+PEI)-BETA*MEI2)
        A2S=DABS(VAU*(EEF+PEF)-MEF2)
        A1=A1S*A1S
        A2=A2S*A2S
        DL13=DLOG(BETA*EEI/(EEI+PEI))*
     *   (3D0*DLOG(EEI)+DLOG(EEI+PEI))/2D0-DLOG(EEI/(EEF+PEF))*
     *   (3D0*DLOG(EEI)+DLOG((EEF+PEF)/A2))/2D0
        DL2=DLOG(BETA/EEI*(EEI+PEI))*DLOG(BETA*EEI*(EEI+PEI)
     *   /(VAU-BETA*(EEI+PEI))/(VAU-BETA*(EEI+PEI)))/2D0
        DREG=DR1+DR2-DR3-DR4+DL2+DL13
        IF (A1S.GT.1D-5) THEN
          DL4=DLOG((EEF+PEF)/EEI)*DLOG(EEI*(EEF+PEF)
     *     /(VAU-(EEF+PEF))/(VAU-(EEF+PEF)))/2D0
          DREG=DREG-DL4
          DL13A=4D0*DLOG(MEI/EEI)*DLOG(BETA*A2S/A1S*(EEF+PEF)
     *     /(EEI+PEI))/2D0+DLOG(BETA*EEI/(EEI+PEI))*DLOG(BETA/A1)/2D0
          DREG=DREG+DL13A
     *     +DLOG(EEI/VAU)*DLOG((VAU*(EEF+PEF)-MEF2)
     *     *(VAU-(EEF+PEF))*(EEI+PEI)/(VAU*(EEI+PEI)-BETA*MEI2)
     *     /(VAU-BETA*(EEI+PEI))/(EEF+PEF))
        ELSEIF (A1S.NE.0D0) THEN
          DL4=DLOG((EEF+PEF)/EEI)*DLOG(EEI*(EEF+PEF)
     *     /(VAU-(EEF+PEF))/(VAU-(EEF+PEF)))/2D0
          DREG=DREG-DL4
          DL13A=2D0*DLOG(MEI/EEI)*DLOG(BETA*A2S*(EEF+PEF)
     *     /(EEI+PEI))+DLOG(BETA*EEI/(EEI+PEI))*DLOG(BETA)/2D0
          DREG=DREG+DL13A
     *     +DLOG(EEI/VAU)*DLOG(DABS((VAU*(EEF+PEF)-MEF2)
     *     *(VAU-(EEF+PEF))*(EEI+PEI)/(VAU-BETA*(EEI+PEI))/(EEF+PEF)))
     *     +A1S/BETA/MEI2*DLOG(A1S)
        ELSE
          DL4=DLOG((EEF+PEF)/EEI)*DLOG(EEI*(EEF+PEF))/2D0
          DREG=DREG-DL4
          DL13A=2D0*DLOG(MEI/EEI)*DLOG(BETA*A2S*(EEF+PEF)/(EEI+PEI))
     *     +DLOG(BETA*EEI/(EEI+PEI))*DLOG(BETA)/2D0
          DREG=DREG+DL13A
     *     +DLOG(EEI/VAU)*DLOG(DABS((VAU*(EEF+PEF)-MEF2)
     *     *(EEI+PEI)/(VAU-BETA*(EEI+PEI))/(EEF+PEF)))
          DARG2=1D0-(EEF+PEF)/VAU
          IF (DABS(DARG2).GT.1D-5) THEN
            DREG=DREG+DLOG((VAU-(EEF+PEF))*(VAU-(EEF+PEF)))
     *       *DLOG((EEF+PEF)/VAU)/2D0
            ELSEIF (DARG2.NE.0D0) THEN
            DREG=DREG-DARG2*DLOG(DABS(DARG2)*VAU)
            ELSE
          ENDIF
        ENDIF
        DR00=2D0*(Q2+2D0*MEI2)/TAU*(DREG
     *           +DABS(DLOG(EEI/(EEI+PEI)))**2D0
     *           -DABS(DLOG(EEI/(EEF+PEF)))**2D0)
        HSDQDV = HSDQDV
     *         + 2D0*DLOG(MEI2/4D0/DELTA2)
     *           *(1D0-(Q2+2D0*MEI2)/TAU*DLOG(BETA))
     *           +2D0*EEI/PEI*DLOG((EEI+PEI)/MEI)
     *           +2D0*EEF/PEF*DLOG((EEF+PEF)/MEF)
     *           +DC0+DR00
      ENDIF
      RETURN
      END
