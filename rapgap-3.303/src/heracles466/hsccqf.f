C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C   INTEGRAND OF KQS TERM (IN CHARGED CURRENT SCATTERING)
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
      FUNCTION HSCCQF(X)
C
C  X(1) -->  XX
C  X(2) -->  Q2   -->  Y
C  X(3) -->  U    -->  XS
C  X(4) -->  A1=(2*K.P)
C  X(5) -->  TS
C
      IMPLICIT DOUBLE PRECISION (A-H,M,O-Z)
      COMMON /HSUNTS/ LUNTES,LUNDAT,LUNIN,LUNOUT,LUNRND
      COMMON /HSOPTN/ INT2(5),INT3(15),ISAM2(5),ISAM3(15),
     *                IOPLOT,IPRINT,ICUT
      COMMON /HSGSW/  SW,CW,SW2,CW2
     *              ,MW,MZ,MH,ME,MMY,MTAU,MU,MD,MS,MC,MB,MT
     *              ,MW2,MZ2,MH2,ME2,MMY2,MTAU2,MU2,MD2,MS2,MC2,MB2,MT2
      COMMON /HSGSW1/ MEI,MEF,MQI,MQF,MEI2,MEF2,MQI2,MQF2,MPRO,MPRO2
      COMMON /HSKNST/ PI,ALPHA,ALP1PI,ALP2PI,ALP4PI,E,GF,SXNORM,SX1NRM
      COMMON /HSKNCC/ SXNRCC,SX1NCC
      COMMON /HSIRCT/ DELEPS,DELTA,EGMIN,IOPEGM
      COMMON /HSELAB/ SP,EELE,PELE,EPRO,PPRO
      COMMON /HSKPXY/ XX,Y
      COMMON /HSCMSP/ EQ,PQ,EEL,PEL,ES,PS,COSE,SINE,OMEGA
      COMMON /HSLABP/ EH,PH,EQH,PQH,ESH,PSH,COSEH,SINEH
      COMMON /HSIKP/  S,T,U,SS,TS,US,DKP,DKPS,DKQ,DKQS
      COMMON /HSCUTS/ XMIN,XMAX,Q2MIN,Q2MAX,YMIN,YMAX,WMIN,GMIN
      COMMON /HSTCUT/ THEMIN,CTHMIN,CTHCON
      COMMON /HSPCUT/ PTMIN,PTXM0
      COMMON /HSXSLM/ XSMIN,XSCUT
      COMMON /HSCUMS/ CQP(12)
      COMMON /HSPDFQ/ QU,QBU,QD,QBD,QS,QBS,QC,QBC,QB,QBB,QT,QBT
      COMMON /HSPARM/ POLARI,LLEPT,LQUA
      COMMON /HSWGTC/ IWEIGS
      DIMENSION X(5)
C
C---X-VALUE
      XX=XMIN+(XMAX-XMIN)*X(1)
      GS=SP-MEI2-MPRO2
      YMAXX=XX*(1D0-4D0*MEI2*MPRO2/GS/GS)/(XX*(1D0+XX*MPRO2/GS)+MEI2/GS)
C---Y-VALUE
      IF(ICUT.LT.3) THEN
C---CUT IN EXTERNALLY DEFINED Q**2(MIN)
        Q2L=Q2MIN
        Q2U=XX*GS
        ELSEIF(ICUT.EQ.3) THEN
C---CUT IN Y
        QQ2MIN=XX*YMIN*GS
C---CUT ON ELECTRON SCATTERING ANGLE (MASSES NEGLECTED)
        YMINTH=1D0/(1D0+XX*CTHCON)
        QT2MIN=XX*YMINTH*GS
C---CUT ON ELECTRON TRANSVERSE MOMENTUM (MASSES NEGLECTED)
        QP2MIN=XX*GS/2D0*(1D0-DSQRT(1D0-PTXM0/XX))
        QP2MAX=XX*GS/2D0*(1D0+DSQRT(1D0-PTXM0/XX))
        YP2MAX=QP2MAX/GS/XX
        Q2L=MAX(Q2MIN,QQ2MIN,QT2MIN,QP2MIN)
        Q2U=XX*MIN(YMAX,YMAXX,YP2MAX)*GS
        Q2U=MIN(Q2U,Q2MAX)
        ELSE
        WRITE(LUNOUT,'(/A,I5/A)') ' WRONG VALUE OF ICUT:',ICUT,
     *                            ' STOP IN HSCCQF'
        STOP
      ENDIF
      DQ2=DMAX1(Q2U-Q2L,0D0)
C---CUT IN W LATER
      Q2=Q2L+X(2)*DQ2
      Y=Q2/(XX*GS)
C
C---EXTERNAL WEIGHT
      IACPT=1
      IF (IWEIGS.GT.0) THEN
        CALL HSWGTX(XX,Y,IACPT)
        IF (IACPT.EQ.0) THEN
          HSCCQF=0D0
          RETURN
        ENDIF
      ENDIF
      CALL HSFIVC(XX,Y)
      CALL HSDELO(XX,Y)
C
      XSMIN=HSXSMN(XX,Y)
      XSMAX=1D0
      XSMINI=XSMIN
      IF(XSMAX.LE.XSMINI) THEN
        HSCCQF=0D0
        RETURN
      ENDIF
C---SUBSTITUTION FOR XS
      M2S =(MEI2+MQI2+MQF2)/Y/SP
      UMIN=Y*SP/MQF2*(DLOG(XSMINI-XX-M2S)
     *               -DLOG(XSMINI-XX-(MEI2+MQI2)/Y/SP) )
      UMAX=Y*SP/MQF2*(DLOG(XSMAX-XX-M2S)
     *               -DLOG(XSMAX-XX-(MEI2+MQI2)/Y/SP) )
      UV=UMIN+(UMAX-UMIN)*X(3)
      EXUV=DEXP(UV*MQF2/Y/SP)
      XS=((XX+(MEI2+MQI2)/Y/SP)*EXUV-XX-M2S)/(EXUV-1D0)
Ch    UMIN=DLOG(XSMINI-XX)
Ch    UMAX=DLOG(XSMAX-XX)
Ch    UV=UMIN+(UMAX-UMIN)*X(3)
Ch    XS=XX+DEXP(UV)
C
      IF(IPRINT.GT.30) THEN
        WRITE(LUNTES,230)SP,XX,Y,XSMIN,XSMAX,XS
230     FORMAT(/,' SP = ',1PD12.3,' X = ',D12.6,'   Y = ',D12.6,/
     F        ,' XSMIN = ',D17.11,'   XSMAX = ',D17.11,'  XS = ',D12.6)
      ENDIF
C
      CALL HSFCMS(XX,Y,XS)
      CALL HSFLAB(XX,Y,XS)
      CALL HSLZK1(ZMIN,ZMAX)
C---NO SUBSTITUTION FOR A1=K.P
      A1MAX=2D0*OMEGA*(EEL-PEL*ZMIN)
      IF (ZMAX.EQ.1D0) THEN
        A1MIN=2D0*OMEGA*MEI2/2D0/EEL
      ELSE
        A1MIN=2D0*OMEGA*(EEL-PEL*ZMAX)
      ENDIF
      A1=A1MIN+(A1MAX-A1MIN)*X(4)
C---NO SUBSTITUTION FOR TS
      CALL HSLTS1(A1,XX,Y,XS,TSMIN,TSMAX,TSM,TSP,CFKP)
      TS=TSMIN+(TSMAX-TSMIN)*X(5)
      SQGRM2=-CFKP*(TS-TSM)*(TS-TSP)
      IF (SQGRM2.LE.0D0) THEN
        HSCCQF=0D0
        RETURN
        ELSE
        SQGRAM=DSQRT(SQGRM2)
      ENDIF
      CALL HSFIV1(XX,Y,XS,A1,TS)
      CALL HSPVER(XS,-TS)
C---CHECK CONDITION ON PHOTON ENERGY
      OMH=(PH*DKQ+PQH*DKP)/(PH*EQH+PQH*EH)
      IF (OMH.LT.DELTA) THEN
        HSCCQF=0D0
        RETURN
      ENDIF
C
C---MATRIX ELEMENT
      DBOS=1D0/(T-MW2)
      CCMSQ=-MQF2*DBOS*DBOS*S*S/DKQS/DKQS
      CCMSB=-MQF2*DBOS*DBOS*U*U/DKQS/DKQS

      IF (LLEPT.EQ.-1) THEN
        CQP(1)=QU*CCMSQ
        CQP(2)=0D0
        CQP(3)=0D0
        CQP(4)=QBD*CCMSB
        CQP(5)=0D0
        CQP(6)=QBS*CCMSB
        CQP(7)=QC*CCMSQ
        CQP(8)=0D0
        CQP(9)=0D0
C        CQP(10)=QBB*CCMSB
Chs: no b-quarks
        CQP(10)= 0D0
        CQP(11)=QT*CCMSQ
        CQP(12)=0D0
      ELSEIF (LLEPT.EQ.1) THEN
        CQP(1)=0D0
        CQP(2)=QBU*CCMSQ
        CQP(3)=QD*CCMSB
        CQP(4)=0D0
        CQP(5)=QS*CCMSB
        CQP(6)=0D0
        CQP(7)=0D0
        CQP(8)=QBC*CCMSQ
C        CQP(9)=QB*CCMSB
Chs: no b-quarks
        CQP(9)=0D0
        CQP(10)=0D0
        CQP(11)=0D0
        CQP(12)=QBT*CCMSQ
      ENDIF

      DO 2 IFL=2,12
        CQP(IFL)=CQP(IFL-1)+CQP(IFL)
 2    CONTINUE
      SUMME=CQP(12)
C
      RPOL=(1D0+LLEPT*POLARI)/2D0
      HSCCQF=SUMME*Y*SX1NCC*RPOL/SQGRAM/XS
     *      *(UMAX-UMIN)*(XS-XX-M2S)*(XS-XX-(MEI2+MQI2)/Y/SP)
Ch   *      *(UMAX-UMIN)*(XS-XX)
     *      *(A1MAX-A1MIN)*(TSMAX-TSMIN)
     *      *(XMAX-XMIN)*DQ2/(XX*SP)
      RETURN
      END
